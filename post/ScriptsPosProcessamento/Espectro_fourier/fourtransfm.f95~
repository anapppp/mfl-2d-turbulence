program fourtransf
!
! programa para calculo de transformadas de Fourier discretas
!
! autor: Ricardo C. de Almeida
! data: 14/04/2011
!
!
! constantes:
! reais:
! pi : pi
!
! variaveis
!
!inteiras:
! n: indice de frequencias
! k: indice de dados medidos
! ndata: numero de dados de entrada
! nmax: indice da frequencia maxima
! ios: verificacao de IOSTAT
!
! reais:
! a: array de dados de entrada
! freq: array das frequencias (ciclos/tempo)
! omega: array das frequencias angulares (rad/tempo)
! periodo: array dos periodos (tempo/ciclo)
! modamp: array dos modulos d as amplitudes dos coeficientes
! espectro: array ds energias do espectro
! pd: periodo da serie de dados
! dt: intervalo de tempo entre os dados!
! dummy1: variavel auxiliar para leitura dos dados
! rndatac: resto da divisao do numero de dadso por 2 (verificacao de paridade)
! hfndata: metado do numero de dados de entrada
! partimag: expoente da parte imaginaria em forma real
! 
! complexas
! fa: array dos coeficientes de Fourier
! sumampk: somador de amplitudes dos coef. de Fourier no loop de k
! termexp: exponencial da serie de fourier
! 
!-------------------------------------------------------
!
implicit none
!
integer:: ndata, nmax, n, k, ios
!
real, allocatable, dimension(:)::a, freq, omega, periodo, modamp, espectro
!
real:: dummy1, rndatac, hfndata, partimag, pd, pi, dt=0.000144
!
complex, allocatable, dimension(:):: fa 
complex:: termexp, sumampk
!
!
! abre arquivos
!
open(unit=5, file="simjato1.u.pto5.flu")
open(unit=6, file="coef_fourtr.pto5.txt")
open(unit=7, file="espectro.pto5.txt")
!
! verifica numero de dados
!
! zera totalizador
!
ndata = 0
!
do
	read(5,*, iostat=ios) dummy1
!
 	if(ios==0) then
    	ndata=ndata+1
    else
      	rewind 5
        exit
    end if
end do
!
! verifica paridade de ndata
!
hfndata=real(ndata)/2.0
rndatac=hfndata-real(int(hfndata))
!
if(rndatac<0.2) then ! caso par
  nmax=ndata/2
else
  ndata=ndata-1
  nmax=(ndata)/2
end if
!
!
! aloca arrays
!
allocate(a(0:ndata-1), freq(0:nmax), omega(0:nmax), periodo(0:nmax), &
        modamp(0:nmax), espectro(0:nmax), fa(0:nmax))
!
! incializa arrays
!
a=0.0
freq=0.0
omega=0.0
periodo=0.0
modamp=0.0
espectro=0.0
fa=(0.0,0.0) 
!
! define pi
!
pi=4.0*atan(1.0)
!
! le dados de entrada
!
do k=0, ndata-1
  	read(5,*) a(k)
end do
!
!------------------------------------------------------------------------
! inicia loop das frequencias (n)
!
do n=0, nmax
!
! zera somador das amplitudes do loop em k
!
	sumampk=(0.0,0.0)
!
! loop dos dados (k)
!
	do k=0, ndata-1
    	partimag=-2.0*pi*real(n)*real(k)/real(ndata)
        termexp=cmplx(0,partimag)
        sumampk=sumampk+a(k)*exp(termexp)
    end do
!
! atribui valor a coeficiente complexo
!
	fa(n)=sumampk/ndata
!
! avanca para proxima frequencia
!
end do
!
!--------------------------------------------------------
!
! calcula periodo dos dados
!
pd=real(ndata)*dt
!
! calcula frequencias e periodos
!
do n=1, nmax
!
	freq(n)=real(n)/pd
  	omega(n)=2.0*pi*freq(n)
    periodo(n)=pd/real(n)
!
end do
!
! calcula modulos das amplitudes e espectro
!
modamp=cabs(fa)
!
espectro=2.0*modamp**2
!
! imprime resultados
!
do n=1, nmax
  write(6,*) fa(n)
!
	write(7,20) n, omega(n), freq(n), periodo(n), espectro(n)
!
	20 format(1x, i4, 4(2x, e10.3))
!
end do       
!
! 
!
!
end program  

